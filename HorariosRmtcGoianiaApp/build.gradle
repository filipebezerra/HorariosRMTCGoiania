apply from: '../buildsystem/ci.gradle'
apply from: '../buildsystem/dependencies.gradle'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

if (project.hasProperty("FbsRelease.signing")
        && new File(project.property("FbsRelease.signing") + ".gradle").exists()) {
    apply from: project.property("FbsRelease.signing") + ".gradle";
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "mx.x10.filipebezerra.horariosrmtcgoiania"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 11
        versionName '2.3'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true

            proguardFile 'proguard-butterknife-7.pro'
            proguardFile 'proguard-crashlytics.pro'
            proguardFile 'proguard-greendao.pro'
            proguardFile 'proguard-jsoup.pro'
            proguardFile 'proguard-square-otto.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
            proguardFile 'proguard-timber.pro'

            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    sourceSets {
        main {
            java.srcDirs = [
                    'src/main/java',
                    'src/main/java-gen',
                    'src/main/test-gen'
            ]
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile libs.appCompat
    compile libs.recyclerView
    compile libs.supportV4
    compile libs.supportAnnotations

    compile libs.butterKnife
    compile libs.timber
    compile libs.nineoldandroids
    compile libs.otto
    compile libs.changeloglib
    compile (libs.materialDialogs){ transitive = true }
    compile libs.greendao
    compile libs.licensesdialog
    compile libs.jsoup

    compile libs.materialNavigationDrawer
    compile libs.snackbar
    compile libs.floatingactionbutton
    compile(libs.crashlytics) { transitive = true }
}