buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "mx.x10.filipebezerra.horariosrmtcgoiania"
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 9
        versionName "2.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            if(rootProject.file('keystore.properties').exists()) {
                def props = new Properties()
                props.load(new FileInputStream(rootProject.file('keystore.properties')))

                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if(rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main {
            java.srcDirs = [
                    'src/main/java',
                    'src/main/java-gen',
                    'src/main/test-gen'
            ]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':MaterialNavigationDrawerLib')
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'uk.co.chrisjenx:calligraphy:2.0.1'
    compile 'de.greenrobot:greendao:1.3.7'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'de.psdev.licensesdialog:licensesdialog:1.7.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.squareup:otto:1.3.6'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }
    compile 'com.jakewharton.timber:timber:2.7.1'
    compile 'com.nispok:snackbar:2.10.5'
    compile 'net.i2p.android.ext:floatingactionbutton:1.8.0'
    compile 'com.afollestad:material-dialogs:0.6.4.7'
}
